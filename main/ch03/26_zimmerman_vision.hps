//-----------------------------------------------------------

///////////////////////////////////////////
// HELPERS
///////////////////////////////////////////

//-----------------------------------------------------------

void PlayEffectVoice(string &in asEntryBase, string &in asCat,int alStartIdx, int alEndIdx, string &in asCallback)
{
	for(int i=alStartIdx; i<=alEndIdx; ++i)
	{
		string sEntry = asEntryBase;
		if(i<10) sEntry += "0";
		sEntry += i;

		AddEffectVoice(sEntry, "", asCat, sEntry, false, "", 0,0 );
	}
	SetEffectVoiceOverCallback(asCallback);
}

//-----------------------------------------------------------

///////////////////////////////////////////
// MAIN EVENTS
///////////////////////////////////////////

//-----------------------------------------------------------

void ResumeEvents()
{
	AddTimer("TimerMainEvents", 0.1f, "TimerMainEvents");
}

//-----------------------------------------------------------

void TimerMainEvents(string &in asTimer)
{
	int lEventNum = GetLocalVarInt("MainEventNum");
	AddLocalVarInt("MainEventNum",1);
	float fNextEventTime = 1.0f;
	bool bPause = false;

	////////////////////////
	// 0: Wait 3 secs before doing anything
	if(lEventNum == 0)
	{
		StartPlayerLookAt("AreaLookAt", 0.5f, 0.5f, "");

		PlaySoundAtEntity("Rain", "26_rain_no3d.snt", "AreaRainSound", 3, false);
		fNextEventTime	= 3;
	}
	////////////////////////
	// 1: Player intro voices
	else if(lEventNum == 1)
	{
		StopPlayerLookAt();
		PlayEffectVoice("CH03L26_DanielDiary08_01_", "Flashbacks", 1,2, "OnVoiceOver");

		bPause = true;
	}
	////////////////////////
	// 2: Fade in, no voices
	else if(lEventNum == 2)
	{
		PlayMusic("amb_extra02.ogg", true, 0.7, 4, 0, false);
		SetPlayerActive(true);
		FadeIn(3);
		fNextEventTime	= 2.0f;
	}
	////////////////////////
	// 3: Play voice on alstadt.
	else if(lEventNum == 3)
	{
		AddTimer("sfx1", 1.0f, "TimerDelaySFX");
		PlayEffectVoice("CH03L26_DanielDiary08_01_", "Flashbacks", 3,3, "OnVoiceOver");
		bPause = true;
	}
	////////////////////////
	// 4: Carriage sounds + music starts
	else if(lEventNum == 4)
	{
		fNextEventTime	= 6.0f;
	}
	////////////////////////
	// 5: Play voice on alstadt.
	else if(lEventNum == 5)
	{
		AddTimer("sfx3", 5.0f, "TimerDelaySFX");
		PlayEffectVoice("CH03L26_DanielDiary08_01_", "Flashbacks", 4,4, "OnVoiceOver");
		bPause = true;
	}
	////////////////////////
	// 6: Play voice on alstadt.
	else if(lEventNum == 6)
	{

		fNextEventTime	= 3.0f;
	}
	////////////////////////
	// 7: Play voice on alstadt.
	else if(lEventNum == 7)
	{
		AddTimer("sfx4", 4.0f, "TimerDelaySFX");
		PlayEffectVoice("CH03L26_DanielDiary08_01_", "Flashbacks", 5,5, "OnVoiceOver");
		bPause = true;
	}
	////////////////////////
	// 8: Play voice on alstadt.
	else if(lEventNum == 8)
	{
		fNextEventTime	= 5.0f;
	}
	////////////////////////
	// 9: Play voice on alstadt.
	else if(lEventNum == 9)
	{
		PlayMusic("26_event_brute.ogg", false, 0.6, 1, 0, false);
		PlayEffectVoice("CH03L26_DanielDiary08_01_", "Flashbacks", 6,6, "OnVoiceOver");
		bPause = true;
	}
	////////////////////////
	// 10: End of vision
	else if(lEventNum == 10)
	{
		StopMusic(6.0f, 0);
		StopSound("Rain", 6);
		FadeOut(6.0f);
		fNextEventTime	= 6.0f;
	}
	////////////////////////
	// 11: New map
	else if(lEventNum == 11)
	{
		ChangeMap("26_torture_nave_redux.map","PlayerStartArea_AfterVision","","");
		return;
	}

	AddDebugMessage("Event:"+lEventNum,false);//+" Time:"+fNextEventTime+" Pause"+bPause?1:0, false);

	if(bPause==false)
		AddTimer("TimerMainEvents", fNextEventTime, "TimerMainEvents");
}

void TimerDelaySFX(string &in asTimer)
{
	if(asTimer == "sfx1")
		PlayGuiSound("26_zimmerman_part1.ogg", 0.7f);
	if(asTimer == "sfx2")
		PlayGuiSound("26_zimmerman_part2.ogg", 0.5f);
	if(asTimer == "sfx3")
		PlayGuiSound("26_zimmerman_part3.ogg", 0.3f);
	if(asTimer == "sfx4")
		PlayGuiSound("26_zimmerman_part4.ogg", 0.7f);

}

//-----------------------------------------------------------

void OnVoiceOver()
{
	ResumeEvents();
}

//-----------------------------------------------------------

///////////////////////////////////////////
// MAIN EVENTS
///////////////////////////////////////////

//-----------------------------------------------------------

//Reduce speed if player enters, increase if he exits
void CollideSpeedDecInOut(string &in asParent, string &in asChild, int alState)
{
	if(alState == 1)
		SetPlayerMoveSpeedMul(0.2);
	else
		SetPlayerMoveSpeedMul(GetLocalVarFloat("PlayerDefualtMoveSpeed"));
}

//-----------------------------------------------------------

void CollideSpeedDec(string &in asParent, string &in asChild, int alState)
{
	AddLocalVarFloat("PlayerDefualtMoveSpeed",-0.1);

	SetPlayerMoveSpeedMul(GetLocalVarFloat("PlayerDefualtMoveSpeed"));
}

//-----------------------------------------------------------

////////////////////////////
// Run first time starting map
void OnStart()
{
    if (GetGlobalVarInt("GiveMeExtraStuff") == 1)
    {
        if (HasItem("lantern") == false)
        {
            GiveItemFromFile("lantern", "lantern.ent");
        }

        for (int i=0; i < 25; ++i)
        {
            GiveItemFromFile("tinderbox", "tinderbox.ent");
            GiveItemFromFile("potion_oil", "potion_oil.ent");
        }
    }

	SetMapDisplayNameEntry("ZimmermanFarm");


	//////////////////////////////
	// Init Player
	SetPlayerMoveSpeedMul(0.45);
	SetPlayerRunSpeedMul(0);

	SetPlayerActive(false);

	SetPlayerJumpDisabled(true);

	SetInventoryDisabled(true);
	SetLanternDisabled(true);
	SetSanityDrainDisabled(true);

	SetRadialBlurStartDist(0.2f);
	FadeRadialBlurTo(0.1f, 1);
	FadeSepiaColorTo(0.5f, 0.025f);

	StopMusic(1.0f, 0);

	FadeOut(0);

	SetGlobalVarInt("FromZimmermanFarm",1); //To play special effects when player gets back.

	/////////////////////////////
	// Var init
	SetLocalVarFloat("PlayerDefualtMoveSpeed", 0.45f);

	/////////////////////////////
	// Collision Areas
	for(int i=1; i<=2; ++i)
	{
		AddEntityCollideCallback("Player", "AreaSpeedDecInOut_"+i, "CollideSpeedDecInOut", false,0);
		AddEntityCollideCallback("Player", "AreaSpeedDec_"+i, "CollideSpeedDec", true,1);
	}

	//////////////////////////////
	// Event timer
	AddTimer("TimerMainEvents", 0.1f, "TimerMainEvents");
}


////////////////////////////
// Run when entering map
void OnEnter()
{
	PreloadSound("26_zimmerman_part1.ogg"); PreloadSound("26_zimmerman_part2.ogg"); PreloadSound("26_zimmerman_part3.ogg"); PreloadSound("26_zimmerman_part4.ogg");
	PreloadSound("26_rain_no3d.snt");
}

////////////////////////////
// Run when leaving map
void OnLeave()
{
	SetupLoadScreen("LoadingText", "Ch03_Zimmerman02", 1, "game_loading_rose.jpg");

	SetInventoryDisabled(false);
	SetLanternDisabled(false);
	SetSanityDrainDisabled(false);

	SetPlayerJumpDisabled(false);

	SetPlayerMoveSpeedMul(1);
	SetPlayerRunSpeedMul(1);
	FadeRadialBlurTo(0,1);
	FadeSepiaColorTo(0, 0.1f);
}
